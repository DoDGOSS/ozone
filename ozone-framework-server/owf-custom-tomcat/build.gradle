import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify


buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '3.3.0'
    }
}


group 'org.ozoneplatform'
version '1.2.3-0'


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'de.undercouch.download'


ext {
    tomcatVersion = '8.5.23'

    tomcatDownloadUrl = "http://archive.apache.org/dist/tomcat/tomcat-8/v${tomcatVersion}/bin"
    tomcatDownloadFilename = "apache-tomcat-${tomcatVersion}-windows-x86.zip"
    tomcatDownloadMd5 = 'd5433dae3cf986cabe1350b450939458'

    downloadDir = "$buildDir/download"

    stagingDir = "$buildDir/staging"
    tomcatStagingDir = "$stagingDir/tomcat"
}


task downloadTomcat(type: Download, group: 'bundle') {
    overwrite false

    src "$tomcatDownloadUrl/$tomcatDownloadFilename"
    dest file("$downloadDir/$tomcatDownloadFilename")
}

task verifyTomcatDownload(type: Verify, group: 'bundle') {
    dependsOn(downloadTomcat)

    src downloadTomcat.dest
    algorithm 'MD5'
    checksum tomcatDownloadMd5
}

task extractTomcat(type: Copy, group: 'bundle') {
    dependsOn(verifyTomcatDownload)

    from zipTree(downloadTomcat.dest)
    into file(stagingDir)
}

task renameTomcatFolder(type: Delete, group: 'bundle') {
    dependsOn(extractTomcat)

    doFirst {
        file("$stagingDir/apache-tomcat-${tomcatVersion}").renameTo(file(tomcatStagingDir))
    }

    delete("$stagingDir/apache-tomcat-${tomcatVersion}")
}

task copyTomcatConfiguration(type: Copy, group: 'bundle') {
    dependsOn(renameTomcatFolder)

    from fileTree("$projectDir/custom")
    into file(tomcatStagingDir)
}

task deleteTomcatExampleWebapps(type: Delete, group: 'bundle') {
    dependsOn(renameTomcatFolder)

    delete("$tomcatStagingDir/webapps/docs",
           "$tomcatStagingDir/webapps/examples",
           "$tomcatStagingDir/webapps/host-manager",
           "$tomcatStagingDir/webapps/manager",
           "$tomcatStagingDir/webapps/ROOT")
}

task buildCustomTomcatZip(type: Zip, group: 'bundle') {
    dependsOn(copyTomcatConfiguration, deleteTomcatExampleWebapps)
    from file(stagingDir)

    archiveName "custom-tomcat.zip"

    destinationDir buildDir
}

artifacts {
    archives(buildCustomTomcatZip.archivePath) {
        type 'zip'
        builtBy buildCustomTomcatZip
    }
}

build.dependsOn(buildCustomTomcatZip)

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

import com.moowork.gradle.node.npm.NpmTask

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'com.moowork.gradle', name: 'gradle-node-plugin', version: '1.3.1'
    }
}

group 'org.ozoneplatform'
version '8.0.0-0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.moowork.node'

ext {
    stagingDir = "$buildDir/staging"
}

node {
    download = false
}

task npmClean(type: NpmTask, group: 'tasks') {
    args = ['run', 'clean']
}

task npmBootstrap(type: NpmTask, group: 'tasks') {
    args = ['run', 'bootstrap']
}

task npmBuild(type: NpmTask, group: 'tasks') {
    dependsOn(npmBootstrap)

    args = ['run', 'build']
}

task copyBuildAssets(type: Copy, group: 'tasks') {
    dependsOn(npmBuild)

    from fileTree("$projectDir/packages/application/build")
    into file(stagingDir)
}

task copyStaticAssets(type: Copy, group: 'tasks') {
    from fileTree("$projectDir/packages/application/public")
    into file(stagingDir)
}

task buildZip(type: Zip, group: 'tasks') {
    dependsOn(copyBuildAssets, copyStaticAssets)
    
    from file(stagingDir)

    archiveName "ozone-client.zip"

    destinationDir buildDir
}

build.dependsOn(buildZip)
clean.dependsOn(npmClean)

artifacts {
    archives(buildZip.archivePath) {
        type 'zip'
        builtBy buildZip
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.3'
}
